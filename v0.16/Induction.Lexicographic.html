<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Induction.Lexicographic</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Lexicographic induction</a>
<a id="133" class="Comment">------------------------------------------------------------------------</a>

<a id="207" class="Keyword">module</a> <a id="214" href="Induction.Lexicographic.html" class="Module">Induction.Lexicographic</a> <a id="238" class="Keyword">where</a>

<a id="245" class="Keyword">open</a> <a id="250" class="Keyword">import</a> <a id="257" href="Data.Product.html" class="Module">Data.Product</a>
<a id="270" class="Keyword">open</a> <a id="275" class="Keyword">import</a> <a id="282" href="Induction.html" class="Module">Induction</a>
<a id="292" class="Keyword">open</a> <a id="297" class="Keyword">import</a> <a id="304" href="Level.html" class="Module">Level</a>

<a id="311" class="Comment">-- The structure of lexicographic induction.</a>

<a id="Σ-Rec"></a><a id="357" href="Induction.Lexicographic.html#357" class="Function">Σ-Rec</a> <a id="363" class="Symbol">:</a> <a id="365" class="Symbol">∀</a> <a id="367" class="Symbol">{</a><a id="368" href="Induction.Lexicographic.html#368" class="Bound">a</a> <a id="370" href="Induction.Lexicographic.html#370" class="Bound">b</a> <a id="372" href="Induction.Lexicographic.html#372" class="Bound">ℓ₁</a> <a id="375" href="Induction.Lexicographic.html#375" class="Bound">ℓ₂</a> <a id="378" href="Induction.Lexicographic.html#378" class="Bound">ℓ₃</a><a id="380" class="Symbol">}</a> <a id="382" class="Symbol">{</a><a id="383" href="Induction.Lexicographic.html#383" class="Bound">A</a> <a id="385" class="Symbol">:</a> <a id="387" class="PrimitiveType">Set</a> <a id="391" href="Induction.Lexicographic.html#368" class="Bound">a</a><a id="392" class="Symbol">}</a> <a id="394" class="Symbol">{</a><a id="395" href="Induction.Lexicographic.html#395" class="Bound">B</a> <a id="397" class="Symbol">:</a> <a id="399" href="Induction.Lexicographic.html#383" class="Bound">A</a> <a id="401" class="Symbol">→</a> <a id="403" class="PrimitiveType">Set</a> <a id="407" href="Induction.Lexicographic.html#370" class="Bound">b</a><a id="408" class="Symbol">}</a> <a id="410" class="Symbol">→</a>
        <a id="420" href="Induction.html#724" class="Function">RecStruct</a> <a id="430" href="Induction.Lexicographic.html#383" class="Bound">A</a> <a id="432" class="Symbol">(</a><a id="433" href="Induction.Lexicographic.html#372" class="Bound">ℓ₁</a> <a id="436" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="438" href="Induction.Lexicographic.html#370" class="Bound">b</a><a id="439" class="Symbol">)</a> <a id="441" href="Induction.Lexicographic.html#375" class="Bound">ℓ₂</a> <a id="444" class="Symbol">→</a> <a id="446" class="Symbol">(∀</a> <a id="449" href="Induction.Lexicographic.html#449" class="Bound">x</a> <a id="451" class="Symbol">→</a> <a id="453" href="Induction.html#724" class="Function">RecStruct</a> <a id="463" class="Symbol">(</a><a id="464" href="Induction.Lexicographic.html#395" class="Bound">B</a> <a id="466" href="Induction.Lexicographic.html#449" class="Bound">x</a><a id="467" class="Symbol">)</a> <a id="469" href="Induction.Lexicographic.html#372" class="Bound">ℓ₁</a> <a id="472" href="Induction.Lexicographic.html#378" class="Bound">ℓ₃</a><a id="474" class="Symbol">)</a> <a id="476" class="Symbol">→</a>
        <a id="486" href="Induction.html#724" class="Function">RecStruct</a> <a id="496" class="Symbol">(</a><a id="497" href="Agda.Builtin.Sigma.html#69" class="Record">Σ</a> <a id="499" href="Induction.Lexicographic.html#383" class="Bound">A</a> <a id="501" href="Induction.Lexicographic.html#395" class="Bound">B</a><a id="502" class="Symbol">)</a> <a id="504" class="Symbol">_</a> <a id="506" class="Symbol">_</a>
<a id="508" href="Induction.Lexicographic.html#357" class="Function">Σ-Rec</a> <a id="514" href="Induction.Lexicographic.html#514" class="Bound">RecA</a> <a id="519" href="Induction.Lexicographic.html#519" class="Bound">RecB</a> <a id="524" href="Induction.Lexicographic.html#524" class="Bound">P</a> <a id="526" class="Symbol">(</a><a id="527" href="Induction.Lexicographic.html#527" class="Bound">x</a> <a id="529" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="531" href="Induction.Lexicographic.html#531" class="Bound">y</a><a id="532" class="Symbol">)</a> <a id="534" class="Symbol">=</a>
  <a id="538" class="Comment">-- Either x is constant and y is &quot;smaller&quot;, ...</a>
  <a id="588" href="Induction.Lexicographic.html#519" class="Bound">RecB</a> <a id="593" href="Induction.Lexicographic.html#527" class="Bound">x</a> <a id="595" class="Symbol">(λ</a> <a id="598" href="Induction.Lexicographic.html#598" class="Bound">y&#39;</a> <a id="601" class="Symbol">→</a> <a id="603" href="Induction.Lexicographic.html#524" class="Bound">P</a> <a id="605" class="Symbol">(</a><a id="606" href="Induction.Lexicographic.html#527" class="Bound">x</a> <a id="608" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="610" href="Induction.Lexicographic.html#598" class="Bound">y&#39;</a><a id="612" class="Symbol">))</a> <a id="615" href="Induction.Lexicographic.html#531" class="Bound">y</a>
    <a id="621" href="Data.Product.html#1352" class="Function Operator">×</a>
  <a id="625" class="Comment">-- ...or x is &quot;smaller&quot; and y is arbitrary.</a>
  <a id="671" href="Induction.Lexicographic.html#514" class="Bound">RecA</a> <a id="676" class="Symbol">(λ</a> <a id="679" href="Induction.Lexicographic.html#679" class="Bound">x&#39;</a> <a id="682" class="Symbol">→</a> <a id="684" class="Symbol">∀</a> <a id="686" href="Induction.Lexicographic.html#686" class="Bound">y&#39;</a> <a id="689" class="Symbol">→</a> <a id="691" href="Induction.Lexicographic.html#524" class="Bound">P</a> <a id="693" class="Symbol">(</a><a id="694" href="Induction.Lexicographic.html#679" class="Bound">x&#39;</a> <a id="697" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="699" href="Induction.Lexicographic.html#686" class="Bound">y&#39;</a><a id="701" class="Symbol">))</a> <a id="704" href="Induction.Lexicographic.html#527" class="Bound">x</a>

<a id="_⊗_"></a><a id="707" href="Induction.Lexicographic.html#707" class="Function Operator">_⊗_</a> <a id="711" class="Symbol">:</a> <a id="713" class="Symbol">∀</a> <a id="715" class="Symbol">{</a><a id="716" href="Induction.Lexicographic.html#716" class="Bound">a</a> <a id="718" href="Induction.Lexicographic.html#718" class="Bound">b</a> <a id="720" href="Induction.Lexicographic.html#720" class="Bound">ℓ₁</a> <a id="723" href="Induction.Lexicographic.html#723" class="Bound">ℓ₂</a> <a id="726" href="Induction.Lexicographic.html#726" class="Bound">ℓ₃</a><a id="728" class="Symbol">}</a> <a id="730" class="Symbol">{</a><a id="731" href="Induction.Lexicographic.html#731" class="Bound">A</a> <a id="733" class="Symbol">:</a> <a id="735" class="PrimitiveType">Set</a> <a id="739" href="Induction.Lexicographic.html#716" class="Bound">a</a><a id="740" class="Symbol">}</a> <a id="742" class="Symbol">{</a><a id="743" href="Induction.Lexicographic.html#743" class="Bound">B</a> <a id="745" class="Symbol">:</a> <a id="747" class="PrimitiveType">Set</a> <a id="751" href="Induction.Lexicographic.html#718" class="Bound">b</a><a id="752" class="Symbol">}</a> <a id="754" class="Symbol">→</a>
      <a id="762" href="Induction.html#724" class="Function">RecStruct</a> <a id="772" href="Induction.Lexicographic.html#731" class="Bound">A</a> <a id="774" class="Symbol">(</a><a id="775" href="Induction.Lexicographic.html#720" class="Bound">ℓ₁</a> <a id="778" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="780" href="Induction.Lexicographic.html#718" class="Bound">b</a><a id="781" class="Symbol">)</a> <a id="783" href="Induction.Lexicographic.html#723" class="Bound">ℓ₂</a> <a id="786" class="Symbol">→</a> <a id="788" href="Induction.html#724" class="Function">RecStruct</a> <a id="798" href="Induction.Lexicographic.html#743" class="Bound">B</a> <a id="800" href="Induction.Lexicographic.html#720" class="Bound">ℓ₁</a> <a id="803" href="Induction.Lexicographic.html#726" class="Bound">ℓ₃</a> <a id="806" class="Symbol">→</a>
      <a id="814" href="Induction.html#724" class="Function">RecStruct</a> <a id="824" class="Symbol">(</a><a id="825" href="Induction.Lexicographic.html#731" class="Bound">A</a> <a id="827" href="Data.Product.html#1352" class="Function Operator">×</a> <a id="829" href="Induction.Lexicographic.html#743" class="Bound">B</a><a id="830" class="Symbol">)</a> <a id="832" class="Symbol">_</a> <a id="834" class="Symbol">_</a>
<a id="836" href="Induction.Lexicographic.html#836" class="Bound">RecA</a> <a id="841" href="Induction.Lexicographic.html#707" class="Function Operator">⊗</a> <a id="843" href="Induction.Lexicographic.html#843" class="Bound">RecB</a> <a id="848" class="Symbol">=</a> <a id="850" href="Induction.Lexicographic.html#357" class="Function">Σ-Rec</a> <a id="856" href="Induction.Lexicographic.html#836" class="Bound">RecA</a> <a id="861" class="Symbol">(λ</a> <a id="864" href="Induction.Lexicographic.html#864" class="Bound">_</a> <a id="866" class="Symbol">→</a> <a id="868" href="Induction.Lexicographic.html#843" class="Bound">RecB</a><a id="872" class="Symbol">)</a>

<a id="875" class="Comment">-- Constructs a recursor builder for lexicographic induction.</a>

<a id="Σ-rec-builder"></a><a id="938" href="Induction.Lexicographic.html#938" class="Function">Σ-rec-builder</a> <a id="952" class="Symbol">:</a>
  <a id="956" class="Symbol">∀</a> <a id="958" class="Symbol">{</a><a id="959" href="Induction.Lexicographic.html#959" class="Bound">a</a> <a id="961" href="Induction.Lexicographic.html#961" class="Bound">b</a> <a id="963" href="Induction.Lexicographic.html#963" class="Bound">ℓ₁</a> <a id="966" href="Induction.Lexicographic.html#966" class="Bound">ℓ₂</a> <a id="969" href="Induction.Lexicographic.html#969" class="Bound">ℓ₃</a><a id="971" class="Symbol">}</a> <a id="973" class="Symbol">{</a><a id="974" href="Induction.Lexicographic.html#974" class="Bound">A</a> <a id="976" class="Symbol">:</a> <a id="978" class="PrimitiveType">Set</a> <a id="982" href="Induction.Lexicographic.html#959" class="Bound">a</a><a id="983" class="Symbol">}</a> <a id="985" class="Symbol">{</a><a id="986" href="Induction.Lexicographic.html#986" class="Bound">B</a> <a id="988" class="Symbol">:</a> <a id="990" href="Induction.Lexicographic.html#974" class="Bound">A</a> <a id="992" class="Symbol">→</a> <a id="994" class="PrimitiveType">Set</a> <a id="998" href="Induction.Lexicographic.html#961" class="Bound">b</a><a id="999" class="Symbol">}</a>
    <a id="1005" class="Symbol">{</a><a id="1006" href="Induction.Lexicographic.html#1006" class="Bound">RecA</a> <a id="1011" class="Symbol">:</a> <a id="1013" href="Induction.html#724" class="Function">RecStruct</a> <a id="1023" href="Induction.Lexicographic.html#974" class="Bound">A</a> <a id="1025" class="Symbol">(</a><a id="1026" href="Induction.Lexicographic.html#963" class="Bound">ℓ₁</a> <a id="1029" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1031" href="Induction.Lexicographic.html#961" class="Bound">b</a><a id="1032" class="Symbol">)</a> <a id="1034" href="Induction.Lexicographic.html#966" class="Bound">ℓ₂</a><a id="1036" class="Symbol">}</a>
    <a id="1042" class="Symbol">{</a><a id="1043" href="Induction.Lexicographic.html#1043" class="Bound">RecB</a> <a id="1048" class="Symbol">:</a> <a id="1050" class="Symbol">∀</a> <a id="1052" href="Induction.Lexicographic.html#1052" class="Bound">x</a> <a id="1054" class="Symbol">→</a> <a id="1056" href="Induction.html#724" class="Function">RecStruct</a> <a id="1066" class="Symbol">(</a><a id="1067" href="Induction.Lexicographic.html#986" class="Bound">B</a> <a id="1069" href="Induction.Lexicographic.html#1052" class="Bound">x</a><a id="1070" class="Symbol">)</a> <a id="1072" href="Induction.Lexicographic.html#963" class="Bound">ℓ₁</a> <a id="1075" href="Induction.Lexicographic.html#969" class="Bound">ℓ₃</a><a id="1077" class="Symbol">}</a> <a id="1079" class="Symbol">→</a>
  <a id="1083" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1099" href="Induction.Lexicographic.html#1006" class="Bound">RecA</a> <a id="1104" class="Symbol">→</a> <a id="1106" class="Symbol">(∀</a> <a id="1109" href="Induction.Lexicographic.html#1109" class="Bound">x</a> <a id="1111" class="Symbol">→</a> <a id="1113" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1129" class="Symbol">(</a><a id="1130" href="Induction.Lexicographic.html#1043" class="Bound">RecB</a> <a id="1135" href="Induction.Lexicographic.html#1109" class="Bound">x</a><a id="1136" class="Symbol">))</a> <a id="1139" class="Symbol">→</a>
  <a id="1143" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1159" class="Symbol">(</a><a id="1160" href="Induction.Lexicographic.html#357" class="Function">Σ-Rec</a> <a id="1166" href="Induction.Lexicographic.html#1006" class="Bound">RecA</a> <a id="1171" href="Induction.Lexicographic.html#1043" class="Bound">RecB</a><a id="1175" class="Symbol">)</a>
<a id="1177" href="Induction.Lexicographic.html#938" class="Function">Σ-rec-builder</a> <a id="1191" class="Symbol">{</a><a id="1192" class="Argument">RecA</a> <a id="1197" class="Symbol">=</a> <a id="1199" href="Induction.Lexicographic.html#1199" class="Bound">RecA</a><a id="1203" class="Symbol">}</a> <a id="1205" class="Symbol">{</a><a id="1206" class="Argument">RecB</a> <a id="1211" class="Symbol">=</a> <a id="1213" href="Induction.Lexicographic.html#1213" class="Bound">RecB</a><a id="1217" class="Symbol">}</a> <a id="1219" href="Induction.Lexicographic.html#1219" class="Bound">recA</a> <a id="1224" href="Induction.Lexicographic.html#1224" class="Bound">recB</a> <a id="1229" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1231" href="Induction.Lexicographic.html#1231" class="Bound">f</a> <a id="1233" class="Symbol">(</a><a id="1234" href="Induction.Lexicographic.html#1234" class="Bound">x</a> <a id="1236" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1238" href="Induction.Lexicographic.html#1238" class="Bound">y</a><a id="1239" class="Symbol">)</a> <a id="1241" class="Symbol">=</a>
  <a id="1245" class="Symbol">(</a><a id="1246" href="Induction.Lexicographic.html#1274" class="Function">p₁</a> <a id="1249" href="Induction.Lexicographic.html#1234" class="Bound">x</a> <a id="1251" href="Induction.Lexicographic.html#1238" class="Bound">y</a> <a id="1253" href="Induction.Lexicographic.html#1668" class="Function">p₂x</a> <a id="1257" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1259" href="Induction.Lexicographic.html#1668" class="Function">p₂x</a><a id="1262" class="Symbol">)</a>
  <a id="1266" class="Keyword">where</a>
  <a id="1274" href="Induction.Lexicographic.html#1274" class="Function">p₁</a> <a id="1277" class="Symbol">:</a> <a id="1279" class="Symbol">∀</a> <a id="1281" href="Induction.Lexicographic.html#1281" class="Bound">x</a> <a id="1283" href="Induction.Lexicographic.html#1283" class="Bound">y</a> <a id="1285" class="Symbol">→</a>
       <a id="1294" href="Induction.Lexicographic.html#1199" class="Bound">RecA</a> <a id="1299" class="Symbol">(λ</a> <a id="1302" href="Induction.Lexicographic.html#1302" class="Bound">x&#39;</a> <a id="1305" class="Symbol">→</a> <a id="1307" class="Symbol">∀</a> <a id="1309" href="Induction.Lexicographic.html#1309" class="Bound">y&#39;</a> <a id="1312" class="Symbol">→</a> <a id="1314" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1316" class="Symbol">(</a><a id="1317" href="Induction.Lexicographic.html#1302" class="Bound">x&#39;</a> <a id="1320" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1322" href="Induction.Lexicographic.html#1309" class="Bound">y&#39;</a><a id="1324" class="Symbol">))</a> <a id="1327" href="Induction.Lexicographic.html#1281" class="Bound">x</a> <a id="1329" class="Symbol">→</a>
       <a id="1338" href="Induction.Lexicographic.html#1213" class="Bound">RecB</a> <a id="1343" href="Induction.Lexicographic.html#1281" class="Bound">x</a> <a id="1345" class="Symbol">(λ</a> <a id="1348" href="Induction.Lexicographic.html#1348" class="Bound">y&#39;</a> <a id="1351" class="Symbol">→</a> <a id="1353" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1355" class="Symbol">(</a><a id="1356" href="Induction.Lexicographic.html#1281" class="Bound">x</a> <a id="1358" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1360" href="Induction.Lexicographic.html#1348" class="Bound">y&#39;</a><a id="1362" class="Symbol">))</a> <a id="1365" href="Induction.Lexicographic.html#1283" class="Bound">y</a>
  <a id="1369" href="Induction.Lexicographic.html#1274" class="Function">p₁</a> <a id="1372" href="Induction.Lexicographic.html#1372" class="Bound">x</a> <a id="1374" href="Induction.Lexicographic.html#1374" class="Bound">y</a> <a id="1376" href="Induction.Lexicographic.html#1376" class="Bound">x-rec</a> <a id="1382" class="Symbol">=</a> <a id="1384" href="Induction.Lexicographic.html#1224" class="Bound">recB</a> <a id="1389" href="Induction.Lexicographic.html#1372" class="Bound">x</a>
                      <a id="1413" class="Symbol">(λ</a> <a id="1416" href="Induction.Lexicographic.html#1416" class="Bound">y&#39;</a> <a id="1419" class="Symbol">→</a> <a id="1421" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1423" class="Symbol">(</a><a id="1424" href="Induction.Lexicographic.html#1372" class="Bound">x</a> <a id="1426" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1428" href="Induction.Lexicographic.html#1416" class="Bound">y&#39;</a><a id="1430" class="Symbol">))</a>
                      <a id="1455" class="Symbol">(λ</a> <a id="1458" href="Induction.Lexicographic.html#1458" class="Bound">y</a> <a id="1460" href="Induction.Lexicographic.html#1460" class="Bound">y-rec</a> <a id="1466" class="Symbol">→</a> <a id="1468" href="Induction.Lexicographic.html#1231" class="Bound">f</a> <a id="1470" class="Symbol">(</a><a id="1471" href="Induction.Lexicographic.html#1372" class="Bound">x</a> <a id="1473" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1475" href="Induction.Lexicographic.html#1458" class="Bound">y</a><a id="1476" class="Symbol">)</a> <a id="1478" class="Symbol">(</a><a id="1479" href="Induction.Lexicographic.html#1460" class="Bound">y-rec</a> <a id="1485" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1487" href="Induction.Lexicographic.html#1376" class="Bound">x-rec</a><a id="1492" class="Symbol">))</a>
                      <a id="1517" href="Induction.Lexicographic.html#1374" class="Bound">y</a>

  <a id="1522" href="Induction.Lexicographic.html#1522" class="Function">p₂</a> <a id="1525" class="Symbol">:</a> <a id="1527" class="Symbol">∀</a> <a id="1529" href="Induction.Lexicographic.html#1529" class="Bound">x</a> <a id="1531" class="Symbol">→</a> <a id="1533" href="Induction.Lexicographic.html#1199" class="Bound">RecA</a> <a id="1538" class="Symbol">(λ</a> <a id="1541" href="Induction.Lexicographic.html#1541" class="Bound">x&#39;</a> <a id="1544" class="Symbol">→</a> <a id="1546" class="Symbol">∀</a> <a id="1548" href="Induction.Lexicographic.html#1548" class="Bound">y&#39;</a> <a id="1551" class="Symbol">→</a> <a id="1553" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1555" class="Symbol">(</a><a id="1556" href="Induction.Lexicographic.html#1541" class="Bound">x&#39;</a> <a id="1559" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1561" href="Induction.Lexicographic.html#1548" class="Bound">y&#39;</a><a id="1563" class="Symbol">))</a> <a id="1566" href="Induction.Lexicographic.html#1529" class="Bound">x</a>
  <a id="1570" href="Induction.Lexicographic.html#1522" class="Function">p₂</a> <a id="1573" class="Symbol">=</a> <a id="1575" href="Induction.Lexicographic.html#1219" class="Bound">recA</a> <a id="1580" class="Symbol">(λ</a> <a id="1583" href="Induction.Lexicographic.html#1583" class="Bound">x</a> <a id="1585" class="Symbol">→</a> <a id="1587" class="Symbol">∀</a> <a id="1589" href="Induction.Lexicographic.html#1589" class="Bound">y</a> <a id="1591" class="Symbol">→</a> <a id="1593" href="Induction.Lexicographic.html#1229" class="Bound">P</a> <a id="1595" class="Symbol">(</a><a id="1596" href="Induction.Lexicographic.html#1583" class="Bound">x</a> <a id="1598" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1600" href="Induction.Lexicographic.html#1589" class="Bound">y</a><a id="1601" class="Symbol">))</a>
            <a id="1616" class="Symbol">(λ</a> <a id="1619" href="Induction.Lexicographic.html#1619" class="Bound">x</a> <a id="1621" href="Induction.Lexicographic.html#1621" class="Bound">x-rec</a> <a id="1627" href="Induction.Lexicographic.html#1627" class="Bound">y</a> <a id="1629" class="Symbol">→</a> <a id="1631" href="Induction.Lexicographic.html#1231" class="Bound">f</a> <a id="1633" class="Symbol">(</a><a id="1634" href="Induction.Lexicographic.html#1619" class="Bound">x</a> <a id="1636" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1638" href="Induction.Lexicographic.html#1627" class="Bound">y</a><a id="1639" class="Symbol">)</a> <a id="1641" class="Symbol">(</a><a id="1642" href="Induction.Lexicographic.html#1274" class="Function">p₁</a> <a id="1645" href="Induction.Lexicographic.html#1619" class="Bound">x</a> <a id="1647" href="Induction.Lexicographic.html#1627" class="Bound">y</a> <a id="1649" href="Induction.Lexicographic.html#1621" class="Bound">x-rec</a> <a id="1655" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="1657" href="Induction.Lexicographic.html#1621" class="Bound">x-rec</a><a id="1662" class="Symbol">))</a>

  <a id="1668" href="Induction.Lexicographic.html#1668" class="Function">p₂x</a> <a id="1672" class="Symbol">=</a> <a id="1674" href="Induction.Lexicographic.html#1522" class="Function">p₂</a> <a id="1677" href="Induction.Lexicographic.html#1234" class="Bound">x</a>

<a id="[_⊗_]"></a><a id="1680" href="Induction.Lexicographic.html#1680" class="Function Operator">[_⊗_]</a> <a id="1686" class="Symbol">:</a> <a id="1688" class="Symbol">∀</a> <a id="1690" class="Symbol">{</a><a id="1691" href="Induction.Lexicographic.html#1691" class="Bound">a</a> <a id="1693" href="Induction.Lexicographic.html#1693" class="Bound">b</a> <a id="1695" href="Induction.Lexicographic.html#1695" class="Bound">ℓ₁</a> <a id="1698" href="Induction.Lexicographic.html#1698" class="Bound">ℓ₂</a> <a id="1701" href="Induction.Lexicographic.html#1701" class="Bound">ℓ₃</a><a id="1703" class="Symbol">}</a> <a id="1705" class="Symbol">{</a><a id="1706" href="Induction.Lexicographic.html#1706" class="Bound">A</a> <a id="1708" class="Symbol">:</a> <a id="1710" class="PrimitiveType">Set</a> <a id="1714" href="Induction.Lexicographic.html#1691" class="Bound">a</a><a id="1715" class="Symbol">}</a> <a id="1717" class="Symbol">{</a><a id="1718" href="Induction.Lexicographic.html#1718" class="Bound">B</a> <a id="1720" class="Symbol">:</a> <a id="1722" class="PrimitiveType">Set</a> <a id="1726" href="Induction.Lexicographic.html#1693" class="Bound">b</a><a id="1727" class="Symbol">}</a>
          <a id="1739" class="Symbol">{</a><a id="1740" href="Induction.Lexicographic.html#1740" class="Bound">RecA</a> <a id="1745" class="Symbol">:</a> <a id="1747" href="Induction.html#724" class="Function">RecStruct</a> <a id="1757" href="Induction.Lexicographic.html#1706" class="Bound">A</a> <a id="1759" class="Symbol">(</a><a id="1760" href="Induction.Lexicographic.html#1695" class="Bound">ℓ₁</a> <a id="1763" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1765" href="Induction.Lexicographic.html#1693" class="Bound">b</a><a id="1766" class="Symbol">)</a> <a id="1768" href="Induction.Lexicographic.html#1698" class="Bound">ℓ₂</a><a id="1770" class="Symbol">}</a> <a id="1772" class="Symbol">{</a><a id="1773" href="Induction.Lexicographic.html#1773" class="Bound">RecB</a> <a id="1778" class="Symbol">:</a> <a id="1780" href="Induction.html#724" class="Function">RecStruct</a> <a id="1790" href="Induction.Lexicographic.html#1718" class="Bound">B</a> <a id="1792" href="Induction.Lexicographic.html#1695" class="Bound">ℓ₁</a> <a id="1795" href="Induction.Lexicographic.html#1701" class="Bound">ℓ₃</a><a id="1797" class="Symbol">}</a> <a id="1799" class="Symbol">→</a>
        <a id="1809" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1825" href="Induction.Lexicographic.html#1740" class="Bound">RecA</a> <a id="1830" class="Symbol">→</a> <a id="1832" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1848" href="Induction.Lexicographic.html#1773" class="Bound">RecB</a> <a id="1853" class="Symbol">→</a>
        <a id="1863" href="Induction.html#912" class="Function">RecursorBuilder</a> <a id="1879" class="Symbol">(</a><a id="1880" href="Induction.Lexicographic.html#1740" class="Bound">RecA</a> <a id="1885" href="Induction.Lexicographic.html#707" class="Function Operator">⊗</a> <a id="1887" href="Induction.Lexicographic.html#1773" class="Bound">RecB</a><a id="1891" class="Symbol">)</a>
<a id="1893" href="Induction.Lexicographic.html#1680" class="Function Operator">[</a> <a id="1895" href="Induction.Lexicographic.html#1895" class="Bound">recA</a> <a id="1900" href="Induction.Lexicographic.html#1680" class="Function Operator">⊗</a> <a id="1902" href="Induction.Lexicographic.html#1902" class="Bound">recB</a> <a id="1907" href="Induction.Lexicographic.html#1680" class="Function Operator">]</a> <a id="1909" class="Symbol">=</a> <a id="1911" href="Induction.Lexicographic.html#938" class="Function">Σ-rec-builder</a> <a id="1925" href="Induction.Lexicographic.html#1895" class="Bound">recA</a> <a id="1930" class="Symbol">(λ</a> <a id="1933" href="Induction.Lexicographic.html#1933" class="Bound">_</a> <a id="1935" class="Symbol">→</a> <a id="1937" href="Induction.Lexicographic.html#1902" class="Bound">recB</a><a id="1941" class="Symbol">)</a>

<a id="1944" class="Comment">------------------------------------------------------------------------</a>
<a id="2017" class="Comment">-- Example</a>

<a id="2029" class="Keyword">private</a>

  <a id="2040" class="Keyword">open</a> <a id="2045" class="Keyword">import</a> <a id="2052" href="Data.Nat.html" class="Module">Data.Nat</a>
  <a id="2063" class="Keyword">open</a> <a id="2068" class="Keyword">import</a> <a id="2075" href="Induction.Nat.html" class="Module">Induction.Nat</a> <a id="2089" class="Symbol">as</a> <a id="2092" class="Module">N</a>

  <a id="2097" class="Comment">-- The Ackermann function à la Rózsa Péter.</a>

  <a id="ackermann"></a><a id="2144" href="Induction.Lexicographic.html#2144" class="Function">ackermann</a> <a id="2154" class="Symbol">:</a> <a id="2156" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="2158" class="Symbol">→</a> <a id="2160" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="2162" class="Symbol">→</a> <a id="2164" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a>
  <a id="2168" href="Induction.Lexicographic.html#2144" class="Function">ackermann</a> <a id="2178" href="Induction.Lexicographic.html#2178" class="Bound">m</a> <a id="2180" href="Induction.Lexicographic.html#2180" class="Bound">n</a> <a id="2182" class="Symbol">=</a>
    <a id="2188" href="Induction.html#1284" class="Function">build</a> <a id="2194" href="Induction.Lexicographic.html#1680" class="Function Operator">[</a> <a id="2196" href="Induction.Nat.html#800" class="Function">N.recBuilder</a> <a id="2209" href="Induction.Lexicographic.html#1680" class="Function Operator">⊗</a> <a id="2211" href="Induction.Nat.html#800" class="Function">N.recBuilder</a> <a id="2224" href="Induction.Lexicographic.html#1680" class="Function Operator">]</a>
          <a id="2236" class="Symbol">(λ</a> <a id="2239" href="Induction.Lexicographic.html#2239" class="Bound">_</a> <a id="2241" class="Symbol">→</a> <a id="2243" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="2244" class="Symbol">)</a>
          <a id="2256" class="Symbol">(λ</a> <a id="2259" class="Symbol">{</a> <a id="2261" class="Symbol">(</a><a id="2262" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>  <a id="2268" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2270" href="Induction.Lexicographic.html#2270" class="Bound">n</a><a id="2271" class="Symbol">)</a>     <a id="2277" class="Symbol">_</a>                   <a id="2297" class="Symbol">→</a> <a id="2299" class="Number">1</a> <a id="2301" href="Agda.Builtin.Nat.html#230" class="Primitive Operator">+</a> <a id="2303" href="Induction.Lexicographic.html#2270" class="Bound">n</a>
             <a id="2318" class="Symbol">;</a> <a id="2320" class="Symbol">(</a><a id="2321" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2325" href="Induction.Lexicographic.html#2325" class="Bound">m</a> <a id="2327" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2329" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="2333" class="Symbol">)</a>  <a id="2336" class="Symbol">(_</a>         <a id="2347" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2349" href="Induction.Lexicographic.html#2349" class="Bound">ackm•</a><a id="2354" class="Symbol">)</a> <a id="2356" class="Symbol">→</a> <a id="2358" href="Induction.Lexicographic.html#2349" class="Bound">ackm•</a> <a id="2364" class="Number">1</a>
             <a id="2379" class="Symbol">;</a> <a id="2381" class="Symbol">(</a><a id="2382" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2386" href="Induction.Lexicographic.html#2386" class="Bound">m</a> <a id="2388" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2390" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="2394" href="Induction.Lexicographic.html#2394" class="Bound">n</a><a id="2395" class="Symbol">)</a> <a id="2397" class="Symbol">(</a><a id="2398" href="Induction.Lexicographic.html#2398" class="Bound">ack[1+m]n</a> <a id="2408" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2410" href="Induction.Lexicographic.html#2410" class="Bound">ackm•</a><a id="2415" class="Symbol">)</a> <a id="2417" class="Symbol">→</a> <a id="2419" href="Induction.Lexicographic.html#2410" class="Bound">ackm•</a> <a id="2425" href="Induction.Lexicographic.html#2398" class="Bound">ack[1+m]n</a>
             <a id="2448" class="Symbol">})</a>
          <a id="2461" class="Symbol">(</a><a id="2462" href="Induction.Lexicographic.html#2178" class="Bound">m</a> <a id="2464" href="Agda.Builtin.Sigma.html#139" class="InductiveConstructor Operator">,</a> <a id="2466" href="Induction.Lexicographic.html#2180" class="Bound">n</a><a id="2467" class="Symbol">)</a>
</pre></body></html>